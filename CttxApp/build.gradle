//apply plugin: 'com.android.application'
apply plugin: 'component-plugin'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.zantong.mobilecttx"

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        //dex突破65535的限制
        multiDexEnabled true
        //阿里推送用
        ndk {
            //选择要添加的对应cpu类型的.so库。
            // abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a', 'x86', 'x86_64'
            abiFilters 'armeabi', 'x86_64'
        }
    }

    sourceSets {
        main.jniLibs.srcDirs = ['libs']
        main.assets.srcDirs = ['assets']

        // setRoot()方法将移动整个组件（包括它的子文件夹）到一个新的文件夹
        instrumentTest.setRoot('tests')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    //执行lint检查，错误或警告会终止构建，将其关掉。
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/README.txt'
        exclude '!META-INF/MANIFEST.MF'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        debug {
            storeFile file("./Users/tzly.jks")
            storePassword "123456"
            keyAlias "jianghw"
            keyPassword "123456"
            v2SigningEnabled false
        }

        release {
            storeFile file("./Users/CTTX.jks")
            storePassword "zantong"
            keyAlias "CTTX"
            keyPassword "zantong"
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            buildConfigField 'boolean', 'isDeta', rootProject.ext.beta.isDeta
            buildConfigField 'String', 'base_url', rootProject.ext.beta.base_url
            buildConfigField 'String', 'bank_img_url', rootProject.ext.beta.bank_img_url
            buildConfigField 'String', 'bank_file_url', rootProject.ext.beta.bank_file_url
            buildConfigField 'String', 'bank_app_url', rootProject.ext.beta.bank_app_url
            buildConfigField 'String', 'bank_mc_url', rootProject.ext.beta.bank_mc_url

            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            manifestPlaceholders = [baidu_API_KEY     : "E6lDRqyaSjj8jR8O5wdz5jpgiT0LOuQn",
                                    alibaba_API_KEY   : "23481202",
                                    alibaba_APP_SECRET: "a51ce60b119cf15b9ab220ed70d8078c"]
        }
        //内部版
        alpha {
            buildConfigField 'boolean', 'isDeta', rootProject.ext.alpha.isDeta
            buildConfigField 'String', 'base_url', rootProject.ext.alpha.base_url
            buildConfigField 'String', 'bank_img_url', rootProject.ext.alpha.bank_img_url
            buildConfigField 'String', 'bank_file_url', rootProject.ext.alpha.bank_file_url
            buildConfigField 'String', 'bank_app_url', rootProject.ext.alpha.bank_app_url
            buildConfigField 'String', 'bank_mc_url', rootProject.ext.alpha.bank_mc_url

            debuggable = true
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [baidu_API_KEY     : "E6lDRqyaSjj8jR8O5wdz5jpgiT0LOuQn",
                                    alibaba_API_KEY   : "23481202",
                                    alibaba_APP_SECRET: "a51ce60b119cf15b9ab220ed70d8078c"]
        }

        release.initWith(android.buildTypes.alpha)
        release {
            buildConfigField 'boolean', 'isDeta', rootProject.ext.release.isDeta
            debuggable = false
        }
    }

    productFlavors {
        //开发者版本
        dev {}
        //华为
        tzly_huawei {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
}

repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //butterknife
    compile 'com.jakewharton:butterknife:7.0.1'
    //阿里云推送
    compile 'com.aliyun.ams:alicloud-android-third-push:3.0.5@aar'
    compile 'com.aliyun.ams:alicloud-android-push:3.0.11@aar'
    compile 'com.aliyun.ams:alicloud-android-utdid:1.1.5.3'
    compile 'com.aliyun.ams:alicloud-android-ut:5.1.0'
    //接口沟通依赖
    compile project(':componentService')
    compile project(':CordovaLib')
}
//自定义组件构建
component {
    isRegisterCompoAuto = true
    applicationName = 'com.zantong.mobilecttx.application.MyApplication'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")&&!requested.name.startsWith("constraint")) {
                details.useVersion '25.3.1'
            }
        }
    }
}
